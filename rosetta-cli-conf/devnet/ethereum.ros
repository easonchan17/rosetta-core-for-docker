// define workflow: workflowName(concurrency), concurrency of request_funds and create_account must be 1
request_funds(1){
  // define job scenarios: find_account
  find_account{
    // define job action: set_variable outputpath=currency input={...}
    currency = {"symbol":"CORE", "decimals":18};
    // define job action: find_balance outputPath=random_account input={...}
    random_account = find_balance({
      "minimum_balance":{
        "value": "0",
        "currency": {{currency}}
      },
      "create_limit":1
    });
  },

  // Create a separate scenario to request funds so that
  // the address we are using to request funds does not
  // get rolled back if funds do not yet exist.
  request{
    loaded_account = find_balance({
      "account_identifier": {{random_account.account_identifier}},
      "minimum_balance":{
        "value": "10000000000000000",
        "currency": {{currency}}
      }
    });
  }
}

create_account(1){
  create{
    network = {"network":"Dev", "blockchain":"Corechain"};
    key = generate_key({"curve_type": "secp256k1"});
    account = derive({
      "network_identifier": {{network}},
      "public_key": {{key.public_key}}
    });

    // If the account is not saved, the key will be lost!
    save_account({
      "account_identifier": {{account.account_identifier}},
      "keypair": {{key}}
    });
  }
}

// define workflow: transfer
transfer(8){
  transfer{
    transfer.network = {"network":"Dev", "blockchain":"Corechain"};
    currency = {"symbol":"CORE", "decimals":18};
    sender = find_balance({
      "minimum_balance":{
        "value": "10000000000000000",
        "currency": {{currency}}
      }
    });

    // Set the recipient_amount as some value <= sender.balance-max_fee
    max_fee = "630000000000000";
    available_amount = {{sender.balance.value}} - {{max_fee}};
    recipient_amount = random_number({"minimum": "1", "maximum": {{available_amount}}});
    print_message({"recipient_amount":{{recipient_amount}}});
    print_message({"available_amount":{{available_amount}}});
    print_message({"max_fee":{{max_fee}}});

    // Find recipient and construct operations
    sender_amount = 0 - {{recipient_amount}};

    recipient = find_balance({
      "not_account_identifier":[{{sender.account_identifier}}],
      "minimum_balance":{
        "value": "0",
        "currency": {{currency}}
      },
      "create_limit": 100,
      "create_probability": 50
    });
    transfer.confirmation_depth = "1";
    transfer.operations = [
      {
        "operation_identifier":{"index":0},
        "type":"CALL",
        "account":{{sender.account_identifier}},
        "amount":{
          "value":{{sender_amount}},
          "currency":{{currency}}
        }
      },
      {
        "operation_identifier":{"index":1},
        "type":"CALL",
        "account":{{recipient.account_identifier}},
        "amount":{
          "value":{{recipient_amount}},
          "currency":{{currency}}
        }
      }
    ];
  }
}

// define workflow: return_funds
return_funds(8){
  // TODO: add suggested fee dry run to ensure account fully cleared
  transfer{
    transfer.network = {"network":"Dev", "blockchain":"Corechain"};
    currency = {"symbol":"CORE", "decimals":18};
    // gasPrice * gas = 30000000000 * 21000
    max_fee = "630000000000000";
    sender = find_balance({
      "minimum_balance":{
        "value": {{max_fee}},
        "currency": {{currency}}
      }
    });

    // Set the recipient_amount as some sender.balance-max_fee
    available_amount = {{sender.balance.value}} - {{max_fee}};
    print_message({"available_amount":{{available_amount}}});
    sender_amount = 0 - {{available_amount}};
    print_message({"sender_amount":{{sender_amount}}});

    // TODO: replace by your faucet account
    // Provide a static address as the recipient and construct operations
    faucet = {"address":"0x57ee5B589CC477d1794736aABc0092dC85c6C495"};
    transfer.confirmation_depth = "1";
    transfer.operations = [
      {
        "operation_identifier":{"index":0},
        "type":"CALL",
        "account":{{sender.account_identifier}},
        "amount":{
          "value":{{sender_amount}},
          "currency":{{currency}}
        }
      },
      {
        "operation_identifier":{"index":1},
        "type":"CALL",
        "account":{{faucet}},
        "amount":{
          "value":{{available_amount}},
          "currency":{{currency}}
        }
      }
    ];
  }
}